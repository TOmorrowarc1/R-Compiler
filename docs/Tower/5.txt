/*Ladies and gentlemen————
The moment you are waiting for—————
Welcome the pride of R language: trait.*/

ITEM Syntax
Crate →
    Item*

Item →
    Function
    | ConstantItem
    | Module
    | Struct
    | Enumeration
    | Trait
    | Implementation

Module →
     mod IDENTIFIER ;
    | mod IDENTIFIER 
    {
        Item*
    }

Function →
    FunctionQualifiers fn IDENTIFIER ( FunctionParameters? )
    FunctionReturnType? ( BlockExpression | ; )//是否允许声明与实现分离？如果不允许，似乎不必考虑匿名参数。

FunctionQualifiers → const?

FunctionParameters → FunctionParam ( , FunctionParam )* ,?

FunctionParam → ( FunctionParamPattern | Type )

FunctionParamPattern → PatternOne : Type

FunctionReturnType → -> Type

Struct → struct IDENTIFIER ( { StructFields? } | ; )

StructFields → StructField ( , StructField )* ,?

StructField → IDENTIFIER : Type

Enumeration →
    enum IDENTIFIER { EnumVariants? }

EnumVariants → EnumVariant ( , EnumVariant )* ,?

EnumVariant →
        IDENTIFIER ( EnumVariantTuple | EnumVariantStruct )? EnumVariantDiscriminant?

EnumVariantTuple → ( TupleFields? )

TupleFields → TupleField ( , TupleField )* ,?

TupleField → Type

EnumVariantStruct → { StructFields? }

EnumVariantDiscriminant → = Expression

ConstantItem →
    const IDENTIFIER : Type ( = Expression )? ;

Trait →
    trait IDENTIFIER ( : TypeParamBounds? )?     
    {
        AssociatedItem*
    }

Implementation → InherentImpl | TraitImpl

InherentImpl →
    impl Type {
        AssociatedItem*
    }

TraitImpl → //需要设计两种类型吗？
    impl !? SimplePath for Type //这个！是什么？
    {
        AssociatedItem*
    }

ConstParam →
    const IDENTIFIER : Type
    ( = BlockExpression | IDENTIFIER | -? LiteralExpression )?

AssociatedItem → ( ConstantItem | Function )


STATEMENT Syntax
Statement →
      ;
    | Item
    | LetStatement
    | ExpressionStatement

LetStatement →
    let PatternOne ( : Type )?
    (
          = Expression
        | = Expressionexcept LazyBooleanExpression or end with a } else BlockExpression
    )? ;

ExpressionStatement →
      ExpressionWithoutBlock ;
    | ExpressionWithBlock ;?


EXPRESSION Syntax
Expression →
      ExpressionWithoutBlock
    | ExpressionWithBlock

ExpressionWithoutBlock →
        LiteralExpression
      | PathExpression
      | OperatorExpression
      | GroupedExpression
      | ArrayExpression
      | IndexExpression
      | TupleExpression
      | TupleIndexingExpression
      | StructExpression
      | CallExpression
      | MethodCallExpression
      | FieldExpression
      | ContinueExpression
      | BreakExpression
      | ReturnExpression
      | UnderscoreExpression

ExpressionWithBlock →
        BlockExpression
      | ConstBlockExpression
      | LoopExpression
      | IfExpression
      | MatchExpression

LiteralExpression →
      CHAR_LITERAL
    | STRING_LITERAL
    | RAW_STRING_LITERAL
    | C_STRING_LITERAL
    | RAW_C_STRING_LITERAL
    | INTEGER_LITERAL
    | true
    | false

PathExpression → Path

BlockExpression →
    {
      Statements?
    }

Statements →
      Statement+
    | Statement+ ExpressionWithoutBlock
    | ExpressionWithoutBlock

ConstBlockExpression → const BlockExpression

OperatorExpression →
    NegationExpression
    | ArithmeticOrLogicalExpression
    | ComparisonExpression
    | LazyBooleanExpression
    | TypeCastExpression
    | AssignmentExpression
    | CompoundAssignmentExpression

NegationExpression →
      - Expression
    | ! Expression

ArithmeticOrLogicalExpression →
      Expression + Expression
    | Expression - Expression
    | Expression * Expression
    | Expression / Expression
    | Expression % Expression
    | Expression & Expression
    | Expression | Expression
    | Expression ^ Expression
    | Expression << Expression
    | Expression >> Expression

ComparisonExpression →
      Expression == Expression
    | Expression != Expression
    | Expression > Expression
    | Expression < Expression
    | Expression >= Expression
    | Expression <= Expression

LazyBooleanExpression →
      Expression || Expression
    | Expression && Expression

TypeCastExpression → Expression as TypeNoBounds

AssignmentExpression → Expression = Expression

CompoundAssignmentExpression →
      Expression += Expression
    | Expression -= Expression
    | Expression *= Expression
    | Expression /= Expression
    | Expression %= Expression
    | Expression &= Expression
    | Expression |= Expression
    | Expression ^= Expression
    | Expression <<= Expression
    | Expression >>= Expression

GroupedExpression → ( Expression )

ArrayExpression → [ ArrayElements? ] //prefix[

ArrayElements →
      Expression ( , Expression )* ,? 
    | Expression ; Expression

IndexExpression → Expression [ Expression ] //infix[

TupleExpression → ( TupleElements? ) //prefix

TupleElements → ( Expression , )+ Expression? //judge after make

TupleIndexingExpression → Expression . TUPLE_INDEX //.infix

StructExpression →
    SimplePath { StructExprFields? } //Path once again

StructExprFields →
    StructExprField ( , StructExprField )* ,?

StructExprField →
        IDENTIFIER
      | IDENTIFIER : Expression

CallExpression → Expression ( CallParams? ) //infix

CallParams → Expression ( , Expression )* ,?

MethodCallExpression → Expression . IDENTIFIER ( CallParams? ) 

FieldExpression → Expression . IDENTIFIER

LoopExpression →
    InfiniteLoopExpression
  | PredicateLoopExpression

InfiniteLoopExpression → loop BlockExpression

PredicateLoopExpression → while Conditions BlockExpression

BreakExpression → break Expression?

ContinueExpression → continue

IfExpression →
    if Conditions BlockExpression
    ( else ( BlockExpression | IfExpression ) )?

Conditions →
      Expressionexcept StructExpression
    | LetChain

LetChain → LetChainCondition ( && LetChainCondition )*

LetChainCondition →
      Expressionexcept ExcludedConditions
    | OuterAttribute* let Pattern = Scrutineeexcept ExcludedConditions

ExcludedConditions →
      StructExpression
    | LazyBooleanExpression
    | RangeExpr
    | RangeFromExpr
    | RangeInclusiveExpr
    | AssignmentExpression
    | CompoundAssignmentExpression

MatchExpression →
    match Scrutinee {
        MatchArms?
    }

Scrutinee → Expressionexcept StructExpression

MatchArms →
    ( MatchArm => ( ExpressionWithoutBlock , | ExpressionWithBlock ,? ) )*
    MatchArm => Expression ,?

MatchArm → Pattern MatchArmGuard?

MatchArmGuard → if Expression

ReturnExpression → return Expression?

UnderscoreExpression → _


PATTERN Syntax
Pattern →
      PatternMult
    | PatternOne

PatternMult → |? PatternOne ( | PatternOne )*

PatternOne →
      LiteralPattern
    | IdentifierPattern
    | WildcardPattern
    | StructPattern
    | TuplePattern
    | GroupedPattern
    | SlicePattern
    | PathPattern

LiteralPattern → -? LiteralExpression

IdentifierPattern → IDENTIFIER ( @ PatternOne )?

WildcardPattern → _

StructPattern →
    SimplePath {
        StructPatternElements?
    }

StructPatternElements →
      StructPatternFields ,?

StructPatternFields →
    StructPatternField ( , StructPatternField )*

StructPatternField →
    IDENTIFIER : Pattern
    | IDENTIFIER

TuplePattern → ( TuplePatternItems? )

TuplePatternItems →
      Pattern ,
    | Pattern ( , Pattern )+ ,?

GroupedPattern → ( Pattern )

SlicePattern → [ SlicePatternItems? ]

SlicePatternItems → Pattern ( , Pattern )* ,?

PathPattern → Path


TYPE Syntax
Type →
      TypeNoBounds
    | ImplTraitType
    | TraitObjectType

TypeNoBounds →
      ParenthesizedType
    | ImplTraitTypeOneBound
    | TraitObjectTypeOneBound
    | Path
    | TupleType
    | NeverType
    | ArrayType
    | SliceType
    | InferredType

ParenthesizedType → ( Type )

NeverType → !

TupleType →
      ( )
    | ( ( Type , )+ Type? )

ArrayType → [ Type ; Expression ]

SliceType → [ Type ]

TraitObjectType → dyn? TypeParamBounds

TraitObjectTypeOneBound → dyn SimplePath

ImplTraitType → impl TypeParamBounds

ImplTraitTypeOneBound → impl SimplePath

InferredType → _

TypeParamBounds → SimplePath ( + SimplePath )* +?


PATH Syntax
Path -> SimplePath | QualifiedPath

SimplePath →
    ::? PathExprSegment ( :: PathExprSegment )*

QualifiedPath → < Type ( as SimplePath )? > ( :: PathExprSegment )+

PathExprSegment →
    IDENTIFIER | super | self | Self | crate